 value←{enforce}(CheckFn AskForNumber)question;flag;input;valid;alias;bool;buff;success
⍝ Operator asking a question and allowing the user to enter a number.\\
⍝ By entering "`∘∘∘`" the user may interrupt `AskForNumber`: this activates a stop.\\
⍝ `CheckFn` is supposed to be a function that gets the input as `⍵` and must return a Boolean
⍝ with a 1 indicating that the input is okay and 0 that it is not. If you don't need/want
⍝ a check function pass `{1}` as left operand.\\
⍝ If the user refuses to enter a number an empty vector is returned.
⍝ However, you might prevent this from happening by specifying a 1 as `⍺` (`enforce`), meaning that
⍝ the user must enter a number in order to continue.
 enforce←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'enforce'
 'The question must contain neither a carriage return nor a line feed'Assert∧/~(⎕UCS 10 13)∊question
 value←⍬
 success←flag←0
 question←{⍵↓⍨+/∧\' '=⍵}question
 :Repeat
     :If 0<⎕NC'AskForNumber_Answers'
         AskForNumber_Answers←(0<≢¨' '~⍨¨AskForNumber_Answers[;1])⌿AskForNumber_Answers
         AskForNumber_Answers[;1]←{⍵↓⍨+/∧\' '=⍵}¨AskForNumber_Answers[;1]
     :AndIf 0<≢AskForNumber_Answers
         :If AliasChar∊question
             (alias question)←{l←⍵⍳AliasChar ⋄ (l↑⍵)(l↓⍵)}question
             flag←0<+/bool←({⍵↑⍨⍵⍳AliasChar}¨AskForNumber_Answers[;1])≡¨⊂alias
         :ElseIf ~flag←0<+/bool←AskForNumber_Answers[;1]≡¨⊂question
             flag←0<+/bool←question∘{⍵≡(≢⍵)↑⍺}¨AskForNumber_Answers[;1]
         :EndIf
         :If flag
             'Multiple pre-prepared answers qualify?!'Assert 1=+/bool
             input←⍕2⊃AskForNumber_Answers[bool⍳1;]
         :EndIf
     :Else
         question←{~'@'∊⍵:⍵ ⋄ ⍵↓⍨⍵⍳'@'}question
     :EndIf
     :If ~flag
         input←⍞,0/⍞←question,': '
         :If '∘∘∘'≡¯3↑input
             (1+⊃⎕LC)⎕STOP⊃⎕SI
             ∘∘∘ ⍝ Deliberate stop caused by user input
         :EndIf
         input←(2+≢question)↓input  ⍝ Plus 2 for the appended ": "
     :EndIf
     :If 0=≢input
     :AndIf ~enforce
         value←⍬
         :Return
     :Else
         (valid value)←⎕VFI input
         :If 1≠≢valid
             ⎕←'Please enter precisely one number'
         :ElseIf valid
             success←CheckFn value
         :Else
             ⎕←'You did not enter a valid number, please retry!'
         :EndIf
     :EndIf
 :Until success
⍝Done
